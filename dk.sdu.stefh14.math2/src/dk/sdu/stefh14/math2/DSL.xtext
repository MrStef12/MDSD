grammar dk.sdu.stefh14.math2.DSL with org.eclipse.xtext.common.Terminals

generate dSL "http://www.sdu.dk/stefh14/math2/DSL"

MathModel:
	declarations+=Declaration*
;

Declaration:
	MathExp | ExternalDef
;

ExternalDef:
	'external' name=ID '(' parameters+=Parameter (',' parameters+=Parameter)* ')' 
;

Parameter:
	type=ID varName=ID
;

MathExp:
	'result' id=STRING 'is' exp=ExpType
;

ExpType returns Expression:
	Exp | ExternalUse
;

Exp returns Expression:
	Factor (('+' {Plus.left=current} | '-' {Minus.left=current}) right=Factor)*
;

Factor returns Expression:
	Primary (('*' {Mult.left=current} | '/' {Div.left=current}) right=Primary)*	
;

Primary returns Expression:
	Number | Parenthesis | VariableBinding | VariableUse
;

VariableUse returns Expression:
	{Var} id=ID
;

VariableBinding returns Expression:
	{Let} 'let' id=ID '=' binding=Exp 'in' body=Exp 'end'
;

Parenthesis returns Expression:
	'(' Exp ')'
;

Number returns Expression:
	{Num} value=INT
;

ExternalUse returns Expression:
	{ExternalUse} external=[ExternalDef] '(' arguments+=ID (',' arguments+=ID)* ')'
;